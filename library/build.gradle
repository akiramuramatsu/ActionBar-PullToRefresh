apply plugin: 'android-library'

dependencies {
    compile 'com.github.castorflex.smoothprogressbar:library:0.4.+@aar'
}

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        // This should be 14, but is 7 because extra-abc/extra-abs depend on this library
        minSdkVersion 7
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}

apply plugin: 'maven'
//apply plugin: 'sigining'

version = "0.0.3"
group = "uk.co.senab"

configurations {
    archives {
        extendsFrom configurations.default
    }
}

//signing {
//    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
//    sign configurations.archives
//}

afterEvaluate { project ->
    uploadArchives {
        configuration = configurations.archives
        repositories.mavenDeployer {
            //beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: project.NEXUS_URL) {
                authentication(userName: project.NEXUS_USERNAME,
                        password: project.NEXUS_PASSWORD)
                pom.artifactId = "ActionBar-PullToRefresh"
            }

            pom.project {
                name 'ActionBar-PullToRefresh'
                packaging 'aar'
                description 'Pull to refresh control used in actionbar and actionbarsherlock'
                url 'https://github.com/foursquare/ActionBar-PullToRefresh'

                scm {
                    url 'scm:git@github.com:foursquare/ActionBar-PullToRefresh.git'
                    connection 'scm:git@github.com:foursquare/ActionBar-PullToRefresh.git'
                    developerConnection 'scm:git@github.com:foursquare/ActionBar-PullToRefresh.git'
                }

                developers {
                    developer {
                        id 'kylefowler'
                        name 'Kyle Fowler'
                        email 'kyle.e.fowler@gmail.com'
                    }
                }
            }
        }
    }

    /*task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.allJava
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }*/

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.allSource
    }

    artifacts {
        archives androidSourcesJar
        //archives androidJavadocsJar
    }
}